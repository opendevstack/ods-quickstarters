// See https://www.opendevstack.org/ods-documentation/ for usage and customization.

@Library('ods-jenkins-shared-library@@shared_library_ref@') _

odsComponentPipeline(
  imageStreamTag: '@ods_namespace@/jenkins-agent-python:@agent_image_tag@',
  testResults : 'tests-results',
  openshiftBuildTimeout: 25,
  openshiftRolloutTimeout: 10,
  branchToEnvironmentMapping: [
    'master': 'dev',
    // 'release/': 'test'
  ]
) { context ->
  def trainingServiceName = "${context.componentId}-training-service"
  def predictionServiceName = "${context.componentId}-prediction-service"
  def buildArgs = [
    "nexusHostWithBasicAuth" : context.nexusHostWithBasicAuth,
    "nexusHostWithoutScheme" : context.nexusHostWithoutScheme,
    "serviceType" : ""
  ]

  stageLint(context)
  stagePrepareBuilds(context)

  odsComponentStageImportOpenShiftImageOrElse(context, [resourceName: trainingServiceName]) {
    buildArgs["serviceType"] = "training"
    odsComponentStageBuildOpenShiftImage(context, [resourceName: trainingServiceName, buildArgs: buildArgs])
  }
  def trainingDeployInfo = odsComponentStageRolloutOpenShiftDeployment(
    context, [selector: "deploymentconfig=${trainingServiceName}"]
  )
  def trainingDeployFirstPodName = trainingDeployInfo["DeploymentConfig/${trainingServiceName}"][0].podName

  stageTrainingUnitTests(context, trainingDeployFirstPodName)
  stageTraining(context, trainingDeployFirstPodName)
  stageTrainingIntegrationTests(context, trainingDeployFirstPodName)
  odsComponentStageScanWithSonar(context)

  odsComponentStageImportOpenShiftImageOrElse(context, [resourceName: predictionServiceName]) {
    buildArgs["serviceType"] = "prediction"
    odsComponentStageBuildOpenShiftImage(context, [resourceName: predictionServiceName, buildArgs: buildArgs])
  }
  odsComponentStageRolloutOpenShiftDeployment(
    context, [selector: "deploymentconfig=${predictionServiceName}"]
  )
}

def stageLint(def context) {
  stage('Lint') {
    // PEP8
    sh """
    virtualenv localenv
    . ./localenv/bin/activate
    pip install pycodestyle requests
    pip check
    pycodestyle --show-source --show-pep8 --max-line-length 100 src/mlservice/model/*
    pycodestyle --statistics -qq --max-line-length 100 src/mlservice/model/*
    pycodestyle --show-source --show-pep8 --max-line-length 100 src/mlservice/services/*
    pycodestyle --statistics -qq --max-line-length 100 src/mlservice/services/*
    pycodestyle --show-source --show-pep8 --max-line-length 100 jenkinsfile_helper.py
    pycodestyle --statistics -qq --max-line-length 100 jenkinsfile_helper.py
    """
  }
}

def stagePrepareBuilds(def context) {
  stage('Prepare Builds') {
    sh 'sh build.sh'
  }
}

def stageTrainingUnitTests(def context, def trainingPod) {
  stage('Training Unit Tests') {
    print("Running Unit Tests in pod ${trainingPod}")
    sh(returnStdout: true, script: "oc exec ${trainingPod} bash ./run_unittests.sh -n ${context.targetProject}")
  }
}

def stageTraining(def context, def trainingPod) {
  stage('Train Model') {
    def trainingRoute = "http://localhost:8080"
    def trainingPassword = sh(returnStdout: true, script:"oc get secret ${context.componentId}-training-secret -n ${context.targetProject} -o jsonpath='{.data.password}'").trim()
    def trainingUsername = sh(returnStdout: true, script:"oc get secret ${context.componentId}-training-secret -n ${context.targetProject} -o jsonpath='{.data.username}'").trim()

    trainingPassword = sh(returnStdout: true, script:"echo \"${trainingPassword}\" | base64 --decode").trim()
    trainingUsername = sh(returnStdout: true, script:"echo \"${trainingUsername}\" | base64 --decode").trim()

    sh "oc port-forward ${trainingPod} 8080 -n ${context.targetProject} &"
    // wait for port-forward to map the ports or for 10s
    sh "timeout 10s bash -c 'until ! curl -v --silent http://localhost:8080 2>&1 | grep -m 1 \"Connection refused\"; do sleep 1 ; done'; echo -e \\\\a"
    // trigger model training
    sh """
      . ./localenv/bin/activate
      python3 jenkinsfile_helper.py --training-service ${trainingRoute} --username ${trainingUsername} --password ${trainingPassword}
    """
    // get the model for the prediction build
    sh "curl -Lv --user ${trainingUsername}:${trainingPassword} ${trainingRoute}/getmodel -o docker/dist/${context.getGitCommit()}"
  }
}

def stageTrainingIntegrationTests(def context, def trainingPod) {
  stage('Training Integration Tests') {
    print("Running Training Integration Tests in pod ${trainingPod}")
    sh(script: "oc -n ${context.targetProject} exec ${trainingPod} bash ./run_integration_tests.sh")
    sh(script: "oc -n ${context.targetProject} exec ${trainingPod} cat tests.xml > testresults.xml")
    sh(script: "oc -n ${context.targetProject} exec ${trainingPod} cat coverage.xml > coverage.xml")
    sh(script: "sed -i -e 's|/app/|$WORKSPACE/src/|g' coverage.xml")
    sh(script: "mkdir -p tests-results && mv testresults.xml tests-results/")
  }
}
